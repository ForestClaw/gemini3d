cmake_minimum_required (VERSION 3.12) # 3.12 find_ROOT
project(test_gem C Fortran)
enable_testing()

get_directory_property(hasParent PARENT_DIRECTORY)
if(NOT hasParent)
  if(NOT realbits)
    set(realbits 64)
  endif()

  list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../cmake/Modules/)
  include(${CMAKE_CURRENT_SOURCE_DIR}/../cmake/compilers.cmake)
  include(${CMAKE_CURRENT_SOURCE_DIR}/../cmake/libraries.cmake)
  include(${CMAKE_CURRENT_SOURCE_DIR}/../cmake/mumps.cmake)
endif()

find_package(MPI REQUIRED COMPONENTS Fortran)

add_executable(testmpi test_mpi.f90)
target_link_libraries(testmpi PRIVATE MPI::MPI_Fortran)
add_test(NAME MPIexist COMMAND testmpi)

set_tests_properties(MPIexist PROPERTIES
  TIMEOUT 5
  FIXTURES_SETUP MPIMUMPS)
#---------------


add_executable(testmumps test_mumps.f90)
target_link_libraries(testmumps PRIVATE
  MUMPS::MUMPS SCALAPACK::SCALAPACK LAPACK::LAPACK MPI::MPI_Fortran)

add_test(NAME MUMPS
  COMMAND ${MPIEXEC_EXECUTABLE} ${MPIEXEC_NUMPROC_FLAG} ${MPIEXEC_MAX_NUMPROCS} $<TARGET_FILE:testmumps>
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})

set_tests_properties(MUMPS PROPERTIES
  TIMEOUT 5
  DEPENDS MPIexist
  FIXTURES_SETUP MPIMUMPS)
